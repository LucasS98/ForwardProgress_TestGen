-- This is an SVL script, see http://cadp.inria.fr/man/svl-lang.html

-- Sanity check: an interesting test, under no scheduler ("none"),
-- should have at least one cycle of EX-ecution steps property
-- NOTE: this is only expected on "<test>_none.lnt" which use SCHEDULER_NONE
property AtLeastOneLassoUnderNoScheduler (model)
is
  "${model}_one_lasso.bcg" = "${model}.bcg" |= with evaluator4
    < true* > < 'EX .*' > @;
  expected TRUE;
end property

-- Sanity check: from any state, there is always a way to reach
-- termination. Again, this should be tested only for "<test>_none.lnt"
-- see first example of: http://cadp.inria.fr/man/mcl4.html#sect16
property CanAlwaysReachTermination (model)
is
  "${model}_no_termination.bcg" = "${model}.bcg" |= with evaluator4
    [ true* . (not "exit") ] < (not "exit")* . "exit" > true;
  expected TRUE;
end property

-- With SCHEDULER_NONE, any test should verify the two sanity check properties
"test_65_none.bcg" = generation of "test_65_none.lnt";
check AtLeastOneLassoUnderNoScheduler(test_65_none);
check CanAlwaysReachTermination(test_65_none);

------------------------------------------------------------------------
-- Look for loops of thread execution steps.
property FreeFromDeadlockLasso (model)
    "There are no deadlock lassos in ${model}"
is
    "${model}_deadlock.bcg" = "${model}.bcg" |= with evaluator4
        < true* > < 'EX .*' > @;
    expected FALSE;
end property

"test_65_obe.bcg" = generation of "test_65_obe.lnt";
check FreeFromDeadlockLasso(test_65_obe);

"test_65_hsa.bcg" = generation of "test_65_hsa.lnt";
check FreeFromDeadlockLasso(test_65_hsa);

"test_65_hsa_obe.bcg" = generation of "test_65_hsa_obe.lnt";
check FreeFromDeadlockLasso(test_65_hsa_obe);

"test_65_lobe.bcg" = generation of "test_65_lobe.lnt";
check FreeFromDeadlockLasso(test_65_lobe);
